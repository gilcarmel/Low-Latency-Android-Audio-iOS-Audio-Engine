apply plugin: 'com.android.model.library'

//Linking 3rd party library as recommended here:
//https://github.com/googlesamples/android-ndk/blob/master/hello-thirdparty/app/build.gradle

def superpowered_sdk_path = file("../../../Superpowered").absolutePath

def superpoweredLibraryName(target) {
    def superpowered_sdk_path = file("../../../Superpowered").absolutePath
    def result
    switch (target) {
        case "armeabi-v7a":
            result = "${superpowered_sdk_path}/libSuperpoweredAndroidARM.a"
            break
        case "arm64-v8a":
            result = "${superpowered_sdk_path}/libSuperpoweredAndroidARM64.a"
            break
        case "x86_64":
            result = "${superpowered_sdk_path}/libSuperpoweredAndroidX86_64.a"
            break
        default:
            result = "${superpowered_sdk_path}/libSuperpoweredAndroidX86.a"
            break
    }
    return result
}

model {
    repositories {
        libs(PrebuiltLibraries) {
            SuperpoweredAndroid {
                headers.srcDir "${superpowered_sdk_path}"
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile file(superpoweredLibraryName(targetPlatform.getName()))
                }
            }
        }
    }

    android {
        compileSdkVersion 23
        buildToolsVersion "23.0.2"

        defaultConfig.with {
            minSdkVersion.apiLevel = 16
            targetSdkVersion.apiLevel = 23
        }


        ndk {
            moduleName = "DTEMixer"
            ldLibs.addAll(['android', 'log', 'OpenSLES'])
            stl = "gnustl_static"
        }

        sources {
            main {
                jni {
                    dependencies {
                        library "SuperpoweredAndroid" linkage "static"
                    }
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.3.0'
    testCompile 'junit:junit:4.12'
}
